module orient_to(origin, normal) {   
      translate(origin)
      rotate([0, 0, atan2(normal.y, normal.x)]) 
      rotate([0, atan2(sqrt(pow(normal.x, 2)+pow(normal.y, 2)),normal.z), 0])
      children();
}

module slice(x,thickness) {
    assign(pa = f(x)) 
    assign(pb = g(x))
    assign(length = norm(pb-pa))
      orient_to(pa,pb-pa)
        cylinder(r=thickness,h=length);   
};

module ruled_surface(step,thickness=1) {
 for (x=[0:step:360])
   hull() {
      slice(x,thickness);
      slice(x+step,thickness);
  }
};

// edge functions - t in range [0 to 360]

function f(t) =  
   [ (Radius+Amplitude*(1+sin(3*t)))*cos(t),
     (Radius+Amplitude*(1+sin(3*t)))*sin(t),
     0
   ];

function g(t) =  
   [ (Radius+Amplitude*(1+sin(3*t)))*cos(t+Phase),
     (Radius+Amplitude*(1+sin(3*t)))*sin(t+Phase),
      Height
   ];

// variables

Radius=25;
Amplitude=4;
Height=100;
Thickness=0.5;
Phase=15;

Step=2;
$fn= 10;

ruled_surface(Step,Thickness);  
